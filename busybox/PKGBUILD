pkgname=busybox
pkgver=1.19.2
pkgrel=66
pkgdesc='Utilities for rescue, embedded and minimal systems'
arch=('i686' 'x86_64')
url='http://busybox.net'
license=('GPL')
options=('emptydirs')
provides=('wget' 'sed' 'diffutils' 'findutils' 'awk' 'gzip' 'unzip' 'tar' 'which'
          'run-parts' 'patch' 'vi' 'procps' 'cpio' 'cron' 'eject' 'runit' 'psmisc' 'bridge-utils'
          'iputils' 'net-tools' 'inetutils' 'man-db' 'logger' 'ntp' 'dhcpcd' 'grep' 'ifenslave')
groups=('base')
conflicts=('cron')
install='busybox.install'
source=(${url}/downloads/${pkgname}-${pkgver}.tar.bz2
        config
        busybox-1.19.2-bb.patch
        runsvdir-start
        script
        man_db.conf)
sha256sums=('ea7ec9b6df70b8c528f4a2b6300e9913431c7223308fb08dfafa7508d75a0cb9'
        '1e8272e2e544ed4fda9d310bb61172880dd68f3e00af773bba5d35470a57f545'
        '98f92c2edbcf61d1bacef783ea8b08cce07051b0a4489ed3f4579296846f89f1'
        'a70d24036a7ae14f213147fe6727fee4320b2090bc59d8a4ffa609d8fdd14cef'
        'd51e911242addaf594bae8c98b61e7711032b66b43455c87656518cb66d05ee6'
        'bf0092798438c18fd3e7f6d9a8a281d6575cf4258d2457b6320d9c6bc8443eae')
symlinks=(/bin/cpio
  /usr/bin/run-parts
  /usr/bin/crontab
  /usr/sbin/crond
  /usr/bin/eject
  /usr/bin/volname
  /usr/bin/chpst
  /usr/bin/runsv
  /usr/bin/runsvdir
  /usr/bin/sv
  /usr/bin/svlogd
  /usr/bin/setuidgid
  /usr/bin/envuidgid
  /usr/bin/envdir
  /usr/bin/softlimit
  /usr/bin/wget
  /bin/sed
  /usr/bin/diff
  /usr/bin/cmp
  /usr/bin/find
  /usr/bin/xargs
  /bin/awk
  /usr/bin/awk
  /bin/gunzip
  /bin/uncompress
  /bin/gzip
  /usr/bin/unzip
  /bin/tar
  /usr/bin/which
  /usr/bin/patch
  /bin/vi
  /bin/kill
  /bin/ps
  /sbin/sysctl
  /usr/bin/free
  /usr/bin/pgrep
  /usr/bin/pkill
  /usr/bin/pmap
  /usr/bin/pwdx
  /usr/bin/top
  /usr/bin/uptime
  /usr/bin/watch
  /usr/bin/fuser
  /usr/bin/killall
  /usr/bin/pstree
  /usr/sbin/brctl
  /bin/ping
  /bin/ping6
  /bin/traceroute
  /bin/traceroute6
  /usr/sbin/arping
  /usr/sbin/tftpd
  /bin/hostname
  /bin/netstat
  /sbin/arp
  /sbin/ifconfig
  /sbin/iptunnel
  /sbin/nameif
  /sbin/route
  /usr/bin/telnet
  /usr/sbin/ftpd
  /usr/sbin/telnetd
  /usr/bin/man
  /sbin/syslogd
  /sbin/klogd
  /bin/logread
  /usr/bin/ntp
  /sbin/udhcpc
  /bin/grep
  /bin/egrep
  /bin/fgrep
  /sbin/ifenslave)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/
  cp ${srcdir}/config .config
  # from Gentoo
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-bb.patch
  sed -i -r \
    -e 's:[[:space:]]?-(Werror|Os|falign-(functions|jumps|loops|labels)=1|fomit-frame-pointer)\>::g' \
    Makefile.flags || exit 1
  sed -i '/^#error Aborting compilation./d' applets/applets.c || exit 1
  sed -i 's:-Wl,--gc-sections::' Makefile
  make
}

package() {
  cd ${srcdir}
  install -D -m0755 ${srcdir}/runsvdir-start ${pkgdir}/sbin/runsvdir-start
  install -D -m0755 ${srcdir}/script ${pkgdir}/usr/share/udhcpc/script
  install -D -m0644 ${srcdir}/man_db.conf ${pkgdir}/etc/man_db.conf

  cd ${srcdir}/${pkgname}-${pkgver}/
  install -m0755 -d ${pkgdir}/bin/
  install -m0755 busybox ${pkgdir}/bin

  install -m0755 -d ${pkgdir}/usr/bin
  install -m0755 -d ${pkgdir}/usr/sbin
  for x in $(seq 0 $((${#symlinks[@]} - 1))); do
    ln -sf /bin/busybox ${pkgdir}/${symlinks[$x]}
  done

  install -o 0 -g 16 -m0750 -d ${pkgdir}/var/spool/cron
  install -o 0 -g 409 -m1730 -d ${pkgdir}/var/spool/cron/crontabs
  for i in $(seq 1 8); do
    install -m0755 -d ${pkgdir}/usr/share/man/man${i}
  done
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

