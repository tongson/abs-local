pkgname=busybox
pkgver=1.19.0
pkgrel=2
pkgdesc='Utilities for rescue and embedded systems'
arch=('i686' 'x86_64')
url='http://busybox.net'
license=('GPL')
makedepends=('make' 'gcc')
conflicts=('cron')
provides=('less' 'wget' 'sed' 'diffutils' 'findutils' 'awk' 'bzip2' 'gzip' 'unzip' 'tar' 'which'
          'run-parts' 'patch' 'vi' 'grep' 'procps' 'cpio' 'cron' 'eject' 'runit' 'psmisc')
groups=('base')
install='busybox.install'
source=(${url}/downloads/${pkgname}-${pkgver}.tar.bz2
        config
        busybox-1.19.0-bb.patch
        busybox-1.19.0-cttyhack.patch
        busybox-1.19.0-less.patch
        busybox-1.19.0-sed.patch
        busybox-1.19.0-swap.patch
        busybox-1.19.0-uncompress.patch
        runsvdir-start)
sha256sums=('19cf44a096d7796800780d6344c4cc5054dac9f50d1c9b7a5c506c4777f7620c'
        'f07a63b1770a7b4800fb120acda5eff85add53039f33bac5ab20b674f437f3f2'
        '98f92c2edbcf61d1bacef783ea8b08cce07051b0a4489ed3f4579296846f89f1'
        'f315e48a9c586fb5e77921eda59c773ba8afbb1e4ac9fd422c66b5c482dca6d2'
        'e0d1a306bf3be7802ef19c0033b278ae2ecf8fd4f271bad7d93209c9c6a0f019'
        'b21ac5eb86519fbcbe0a74fc531cb707a878e988b6fdb298be6083760e04fb89'
        'd9d70a607d877646b4e2087ddf4c3ad14d5bcb2b5973b33fed30ee8e6d69b40d'
        '74cd12b54d47a2ce0e418d8d15cd0be1649bbec5951d9000b5275683f9b0623d'
        'a70d24036a7ae14f213147fe6727fee4320b2090bc59d8a4ffa609d8fdd14cef')
symlinks=(/bin/less
  /bin/cpio
  /usr/bin/run-parts
  /usr/bin/crontab
  /usr/sbin/crond
  /usr/bin/eject
  /usr/bin/volname
  /usr/bin/chpst
  /usr/bin/runsv
  /usr/bin/runsvdir
  /usr/bin/sv
  /usr/bin/svlogd
  /usr/bin/setuidgid
  /usr/bin/envuidgid
  /usr/bin/envdir
  /usr/bin/softlimit
  /usr/bin/wget
  /bin/sed
  /usr/bin/diff
  /usr/bin/cmp
  /usr/bin/find
  /usr/bin/xargs
  /bin/awk
  /usr/bin/awk
  /bin/bunzip2
  /bin/bzip2
  /bin/gunzip
  /bin/uncompress
  /bin/gzip
  /usr/bin/unzip
  /bin/tar
  /usr/bin/which
  /usr/bin/patch
  /bin/vi
  /bin/grep
  /bin/kill
  /bin/ps
  /sbin/sysctl
  /usr/bin/free
  /usr/bin/pgrep
  /usr/bin/pkill
  /usr/bin/pmap
  /usr/bin/pwdx
  /usr/bin/top
  /usr/bin/uptime
  /usr/bin/watch
  /usr/bin/fuser
  /usr/bin/killall
  /usr/bin/pstree)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp "${srcdir}/config" .config
  # from Gentoo
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-bb.patch
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-cttyhack.patch
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-less.patch
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-sed.patch
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-swap.patch
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-uncompress.patch
  sed -i -r \
    -e 's:[[:space:]]?-(Werror|Os|falign-(functions|jumps|loops|labels)=1|fomit-frame-pointer)\>::g' \
    Makefile.flags || exit 1
  sed -i '/^#error Aborting compilation./d' applets/applets.c || exit 1
  sed -i 's:-Wl,--gc-sections::' Makefile
  make
}

package() {
  cd "${srcdir}"
  install -m0755 -d ${pkgdir}/sbin/
  install -m0755 ${srcdir}/runsvdir-start ${pkgdir}/sbin

  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m0755 -d ${pkgdir}/bin/
  install -m0755 busybox ${pkgdir}/bin

  install -m0755 -d ${pkgdir}/usr/bin
  install -m0755 -d ${pkgdir}/usr/sbin
  for x in $(seq 0 $((${#symlinks[@]} - 1))); do
    ln -sf /bin/busybox ${pkgdir}/${symlinks[$x]}
  done

  install -o 0 -g 16 -m0750 -d ${pkgdir}/var/spool/cron
  install -o 0 -g 409 -m1730 -d ${pkgdir}/var/spool/cron/crontabs
}

